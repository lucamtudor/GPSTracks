apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'application'

version = '0.1.0'

mainClassName = 'ro.tudorluca.gpstracks.webapi.GPSTracksApplication'

//ext.dropwizard_version = '0.8.0-rc2-SNAPSHOT'
ext.dropwizard_version = '0.7.1'

configurations {
    capsule
}

run {
    systemProperty "dropwizard.config", "build/resources/main/dev_config.yml"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    capsule 'co.paralleluniverse:capsule:0.10.0'
    runtime 'com.h2database:h2:1.4.182'

    compile 'com.google.guava:guava:18.0'
    compile("io.dropwizard:dropwizard-core:${dropwizard_version}",) {
        exclude group: 'com.google.guava'
    }
    compile "io.dropwizard:dropwizard-hibernate:${dropwizard_version}"
    compile "io.dropwizard:dropwizard-db:${dropwizard_version}"
    compile "io.dropwizard:dropwizard-assets:${dropwizard_version}"
    compile "io.dropwizard:dropwizard-jdbi:${dropwizard_version}"

    compile 'mysql:mysql-connector-java:5.1.34'
    compile 'com.google.inject:guice:4.0-beta5'
    compile(group: 'com.hubspot.dropwizard', name: 'dropwizard-guice', version: '0.7.2') {
//        exclude(module: 'javassist')
    }

    compile 'org.assertj:assertj-core:1.7.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "gpstracks-webapi.jar"

    from jar // embed our application jar
    from { configurations.runtime } // embed dependencies
    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' }

    from { sourceSets.main.resources }

    manifest {
        attributes(
                'Main-Class': 'Capsule',
                'Application-Class': mainClassName,
                'Application-Version': version,
                'Min-Java-Version': '1.8.0',
                'JVM-Args': run.jvmArgs.join(' '),
                'System-Properties': (run.systemProperties + ["dropwizard.config": '$CAPSULE_DIR/dev_config.yml']).collect { k, v -> "$k=$v" }.join(' '),
        )
    }
}
